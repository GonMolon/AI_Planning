(define (domain redflix_domain)
  (:requirements :strips :typing :adl :equality :fluents)
  (:types content
          time)
  (:constants never somewhen before yesterday today - time)
  
  (:predicates (precedes ?x - content ?y - content)
               (parallel ?x - content ?y - content)
               (watched ?x - content ?w - time)
               (to_watch ?x - content)
               (watchedsmthtoday))
  
  (:functions (contents_today)
              (minutes_today)
              (totals_watched)
              (duration ?x - content))

  (:action watch_content
    :parameters (?x - content)
    :precondition (and (watched ?x never)
                       (< (contents_today) 3)
                       (forall (?y - content)
                           (and (or (not (precedes ?y ?x))
                                    (and (not (watched ?y today))
                                         (watched ?y somewhen)
                                    )
                                )
                                (or (not (parallel ?x ?y))
                                    (not (watched ?y before))
                                )
                           )
                       )
                  )
    :effect (and (watched ?x today)
                 (watched ?x somewhen)
                 (not (watched ?x never))
                 (increase (contents_today) 1)
                 (increase (totals_watched) 1)
                 (watchedsmthtoday))
  )

  (:action next_day
    :parameters ()
    :precondition (watchedsmthtoday)
    :effect (and (decrease (contents_today) (contents_today))
                 (not (watchedsmthtoday))
                 (forall (?x - content)
                    (when (watched ?x yesterday) (and (not (watched ?x yesterday)) (watched ?x before)))
                 )
                 (forall (?x - content)
                    (when (watched ?x today) (and (not (watched ?x today)) (watched ?x yesterday)))
                 )
            )
  )
)

